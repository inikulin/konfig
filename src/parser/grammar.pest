value_assignment = { path ~ "=" ~ rhs }

// Path
//--------------------------------------------------------------------------------------------
path = ${ path_start ~ path_item? ~ ( path_sep ~ path_item )* }
path_start = @{ ">" ~ SPACE* }
path_sep = @{ SPACE* ~ ">" ~ line_wrap_path_sep? ~ SPACE* } 
line_wrap_path_sep = @{ SPACE* ~ NEWLINE ~ ">" }
path_item = { field_name | enum_variant | map_key | index }

field_name = @{ ASCII_ALPHA ~ ( ASCII_ALPHANUMERIC | "_" )* }

enum_variant = ${ "`" ~ enum_variant_ident ~ "`" }
enum_variant_ident = @{ field_name }

map_key = { "[" ~ map_key_literal ~ "]" }
map_key_literal = { single_quoted_string | double_quoted_string } 

index = { "[" ~ index_digits ~ "]" }
index_digits = ${ "0" | ( ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* ) }


// RHS
//--------------------------------------------------------------------------------------------
rhs = { sequence_of_primitives | primitive }

sequence_of_primitives = { "[" ~ sequence_of_primitives_values? ~ "]" }
sequence_of_primitives_values = { primitive ~ ( "," ~ primitive )* ~ ","? }


// Primitives
//--------------------------------------------------------------------------------------------
primitive = { 
    null | boolean | float | neg_int | pos_int | single_quoted_string | double_quoted_string |
    raw_string | enum_variant
}

null = { "null" }

boolean = { boolean_true | boolean_false }
boolean_true = { "true" }
boolean_false = { "false" }

pos_int = ${ ( "0x" ~ hex_digits ) | dec_digits }
hex_digits = { ASCII_HEX_DIGIT+ }
dec_digits = { ASCII_DIGIT+ }

neg_int = ${ "-" ~ pos_int }

float = ${ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* ~ exponent? }
exponent = @{ ( "e" | "E" ) ~ ( "+" | "-" )? ~ ASCII_DIGIT+ }

double_quoted_string = ${ "\"" ~ double_quoted_string_content ~ "\"" }
double_quoted_string_content = ${ ( double_quoted_string_text | esc )* }
double_quoted_string_text = { ( !("\"" | "\\") ~ ANY )+ }

single_quoted_string = ${ "'" ~ single_quoted_string_content ~ "'" }
single_quoted_string_content = ${ ( single_quoted_string_text | esc )* }
single_quoted_string_text = { ( !("'" | "\\") ~ ANY )+ }

raw_string = ${ raw_string_start ~ raw_string_text ~ raw_string_end }
raw_string_start = ${ "```" ~ raw_string_lang_ident? ~ SPACE* ~ NEWLINE }
raw_string_end = ${ NEWLINE ~ "```" }
raw_string_text = { ( !raw_string_end ~ ANY )+ }
raw_string_lang_ident = ${ ( ASCII_ALPHANUMERIC | "_" )+ }

esc = ${ "\\" ~ ( esc_alias | ( "u" ~ esc_unicode ) ) }
esc_alias = { "\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | NEWLINE }
esc_unicode = { ASCII_HEX_DIGIT{1, 6} }

SPACE = _{ " " | "\t" }
WHITESPACE = _{ SPACE | NEWLINE }